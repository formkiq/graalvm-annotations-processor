plugins {
	id 'java'
	id 'maven-publish'
	id 'signing'
	id 'checkstyle'
  	id 'com.diffplug.spotless' version '6.17.0'
  	id 'com.github.spotbugs'  version '5.0.14'
    id 'com.github.ben-manes.versions' version '0.46.0'
}

group 'com.formkiq'
version '1.4.0-SNAPSHOT'

dependencies {
	annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'
	compileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'
	compileOnly group: 'com.google.auto.service', name: 'auto-service-annotations', version: '1.0.1'

    implementation group: 'com.formkiq', name: 'graalvm-annotations', version: '1.2.0'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
	testImplementation group: 'junit', name: 'junit', version:'4.+'
	testImplementation group: 'com.google.testing.compile', name: 'compile-testing', version: '0.21.0'
}

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
}

spotless {
  java {
    googleJavaFormat()
  }
}

spotbugs {
  excludeFilter = file("$rootDir/config/gradle/spotbugs-exclude.xml")
}
  
spotbugsMain {
  reports {
    html {
      enabled = true
    }
  }
}

checkstyle {
	toolVersion '8.29'
	configFile file("config/checkstyle/checkstyle.xml")
	configProperties = [project_loc: "${projectDir}"]
	ignoreFailures = false
  	maxWarnings = 0
}

repositories {
	mavenLocal()
    mavenCentral()
}

check {
	  dependsOn(tasks.publishToMavenLocal)
}

java {
    withJavadocJar()
    withSourcesJar()
    
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(11))
	}
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

artifacts {
    archives jar
	
    archives javadocJar
    archives sourcesJar
}

afterEvaluate {
	   tasks.getByName('spotlessCheck').dependsOn(tasks.getByName('spotlessApply'))
}

publishing {
	publications {
	    mavenJava(MavenPublication) {
	        from components.java
	        
	        pom {
                name = 'FormKiQ Lambda Runtime Graalvm Annotations'
                description = 'Lambda Runtime Graalvm Annotations'
                url = 'https://github.com/formkiq/graalvm-annotations-processor'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mfriesen'
                        name = 'Mike Friesen'
                        email = 'mike@formkiq.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/formkiq/graalvm-annotations-processor.git'
                    developerConnection = 'scm:git:ssh://github.com/formkiq/graalvm-annotations-processor.git'
                    url = 'https://github.com/formkiq/graalvm-annotations-processor.git'
                }
            }
	    }
	}
	repositories {
		maven {
		  credentials {
		    username project.repoUser
		    password project.repoPassword
		  }
		  // url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
		  url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
}

signing {
    sign publishing.publications.mavenJava
}

check {
	dependsOn(tasks.publishToMavenLocal)
}

spotlessJavaCheck.dependsOn spotlessJavaApply
