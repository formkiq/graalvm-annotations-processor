import com.vanniktech.maven.publish.SonatypeHost

plugins {
	id 'java'
	id 'checkstyle'
    id 'com.vanniktech.maven.publish' version '0.32.0'
  	id 'com.diffplug.spotless' version '7.0.4'
  	id 'com.github.spotbugs'  version '6.1.13'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

group 'com.formkiq'
version '1.5.0'

dependencies {
	annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.1.1'
	compileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.1.1'
	compileOnly group: 'com.google.auto.service', name: 'auto-service-annotations', version: '1.1.1'

    implementation group: 'com.formkiq', name: 'graalvm-annotations', version: '1.2.0'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.13.1'
	testImplementation group: 'junit', name: 'junit', version:'4.+'
	testImplementation group: 'com.google.testing.compile', name: 'compile-testing', version: '0.21.0'
}

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
}

spotless {
    java {
        eclipse().configFile project.rootProject.file("spotless.eclipseformat.xml")
        licenseHeaderFile project.rootProject.file("LICENSE_SHORT")
    }
}

spotbugs {
  excludeFilter = file("$rootDir/config/gradle/spotbugs-exclude.xml")
}

spotbugsMain {
    reports {
        html {
            required.set(true)
        }
    }
}

checkstyle {
    toolVersion = '10.12.1'
    configFile file("config/checkstyle/checkstyle.xml")
    configProperties = [project_loc: "${projectDir}"]
    maxWarnings = 0
    maxErrors = 0
}

repositories {
	mavenLocal()
    mavenCentral()
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(17))
	}
}

mavenPublishing {

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()

    pom {
        name = "FormKiQ Lambda Runtime Graalvm"
        description = "Lambda Runtime Graalvm"
        inceptionYear = "2020"
        url = "https://github.com/formkiq/lambda-runtime-graalvm"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = 'mfriesen'
                name = 'Mike Friesen'
            }
        }
        scm {
            url = 'https://github.com/formkiq/lambda-runtime-graalvm.git'
            connection = 'scm:git:git://github.com/formkiq/lambda-runtime-graalvm.git'
            developerConnection = 'scm:git:ssh://github.com/formkiq/lambda-runtime-graalvm.git'
        }
    }
}

compileJava.dependsOn(tasks.spotlessApply)
check.dependsOn(tasks.publishToMavenLocal)
